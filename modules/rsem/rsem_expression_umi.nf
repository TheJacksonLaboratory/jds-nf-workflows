process RSEM_EXPRESSION {
    tag "$sampleID"

    cpus 12
    memory 90.GB
    time 24.h
    errorStrategy {(task.exitStatus == 140) ? {log.info "\n\nError code: ${task.exitStatus} for task: ${task.name}. Likely caused by the task wall clock: ${task.time} or memory: ${task.memory} being exceeded.\nAttempting orderly shutdown.\nSee .command.log in: ${task.workDir} for more info.\n\n"; return 'finish'}.call() : 'finish'}

    container 'quay.io/jaxcompsci/rsem_bowtie2_star:0.1.0'

    publishDir "${params.pubdir}/${sampleID}", pattern: "*results*", mode:'copy'

    input:
    tuple val(sampleID), path(bam), val(read_length), val(strand_setting)
    val(rsem_ref_path)
    val(rsem_star_prefix)

    output:
    tuple val(sampleID), path("*.stat/*.cnt"), emit: rsem_cnt
    tuple val(sampleID), path("*genes.results"), emit: rsem_genes
    tuple val(sampleID), path("*isoforms.results"), emit: rsem_isoforms
    tuple val(sampleID), path("*.transcript.bam"), emit: transcript_bam
    
    script:

    if (params.read_type == "PE"){
        frag=""
        read_type="--paired-end"
    }
    if (params.read_type == "SE"){
        frag="--fragment-length-mean ${params.fragment_length_mean} --fragment-length-sd ${params.fragment_length_sd}"
        read_type=""
    }

    def output_name = bam.getName() == "${sampleID}.transcript.bam" ? "${sampleID}.rsem_out" : "${sampleID}"
    // Note: If the input BAM, shares the same name as output from RSEM: i.e., ${sampleID}.transcript.bam, 
    // RSEM will fail as the input gets potentially overwritten. This avoids naming collisions. 


    if (strand_setting == "reverse_stranded") {
        prob="--forward-prob 0"
    }

    if (strand_setting == "forward_stranded") {
        prob="--forward-prob 1"
    }

    if (strand_setting == "non_stranded") {
        prob="--forward-prob 0.5"
    }

    read_length = read_length.toInteger()

    if( read_length >= 65 && read_length <= 85) {
        rsem_ref_files = file("${rsem_ref_path}/STAR/${rsem_star_prefix}_75/*").collect { "$it" }.join(' ')
    } else if( read_length >= 90 && read_length <= 110 ) {
        rsem_ref_files = file("${rsem_ref_path}/STAR/${rsem_star_prefix}_100/*").collect { "$it" }.join(' ')
    } else if( read_length >= 115 && read_length <= 135 ) {
        rsem_ref_files = file("${rsem_ref_path}/STAR/${rsem_star_prefix}_125/*").collect { "$it" }.join(' ')
    } else if( read_length >= 140 && read_length <= 160 ) {
        rsem_ref_files = file("${rsem_ref_path}/STAR/${rsem_star_prefix}_150/*").collect { "$it" }.join(' ')
    } else {
        log.info("\nUnsupported read length " + read_length + " in RSEM with STAR. RSEM will now fail gracefully.\n\n")
        rsem_ref_files = 'error'
    }

    """
    if [ "${rsem_ref_files}" = "error" ]; then exit 1; fi

    ln -s -f ${rsem_ref_files} . 

    rsem_ref_name=\$(basename \$(find . -name "*.transcripts.fa") .transcripts.fa)

    rsem-calculate-expression -p $task.cpus \
    ${prob} \
    ${read_type} \
    ${frag} \
    --alignments \
    ${bam} \
    \${rsem_ref_name} \
    ${output_name}
    """
}

/*
Using an alternative aligner


To use an alternative alignment program, align the input reads against the file reference_name.idx.fa generated by rsem-prepare-reference, and format the alignment output in SAM/BAM/CRAM format. 
Then, instead of providing reads to rsem-calculate-expression, specify the --alignments option and provide the SAM/BAM/CRAM file as an argument.

RSEM requires the alignments of a read to be adjacent. For paired-end reads, RSEM also requires the two mates of any alignment be adjacent. To check if your SAM/BAM/CRAM file satisfy the requirements, run


     rsem-calculate-expression --paired-end \
                               --alignments \
                               -p 8 \
                               /data/mmliver_paired_end_quals.bam \
                               /ref/mouse_125 \
                               mmliver_paired_end_quals


rsem-sam-validator <input.sam/input.bam/input.cram>

If your file does not satisfy the requirements, you can use convert-sam-for-rsem to convert it into a BAM file which RSEM can process. Run

convert-sam-for-rsem --help
*/
